plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

dependencies {
    //noinspection GradleDependency
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation project(':tesseract4android')
    implementation platform('com.google.firebase:firebase-bom:27.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.toastfix:toastcompatwrapper:1.2.0'
}

def getVersionCode = { ->
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
        standardOutput = code
    }
    return Integer.parseInt(code.toString().split("\n")[0])
}

def getVersionName = { ->
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--always', '--tags', '--dirty'
        standardOutput = code
    }
    def v = "1.2.3-${code.toString().split("\n")[0].trim()}"
    return  v
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.aowtest.myapplication"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("bool", "DEBUG", "false")
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
            ndk {
                debugSymbolLevel "FULL"
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-debug.pro'
            versionNameSuffix "-debug"
            resValue("bool", "DEBUG", "true")
        }
    }
    lintOptions {
        disable('AllowBackup', 'GoogleAppIndexingWarning', 'MissingApplicationIcon')
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }
}